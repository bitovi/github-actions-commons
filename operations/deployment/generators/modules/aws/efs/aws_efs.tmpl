locals {
  # mount_target: Fall-Through variable that checks multiple layers of EFS zone map selection
  mount_target      = var.aws_efs_zone_mapping != null ? local.user_zone_mapping : (var.aws_efs_create_ha ? local.ha_zone_mapping : (length(local.ec2_zone_mapping) > 0 ? local.ec2_zone_mapping : local.no_zone_mapping))
  # mount_efs: Fall-Through variable that checks multiple layers of EFS creation and if any of them are active, sets creation to active.
  mount_efs         = var.aws_efs_mount_id != null ? true : (local.create_efs ? true : false)

  # replica_destination: Checks whether a replica destination exists otherwise sets a default
  replica_destination  = var.aws_efs_replication_destination != null ? var.aws_efs_replication_destination : data.aws_region.current.name
  # create_mount_targets: boolean on whether to create mount_targets
  create_mount_targets = var.aws_efs_create || var.aws_efs_create_ha ? local.mount_target : {}
  # create_efs: boolean, checks whether to create an EFS or not
  create_efs           = var.aws_efs_create || var.aws_efs_create_ha ? true : false 
}

# ---------------------CREATE--------------------------- #
resource "aws_efs_file_system" "efs" {
  # File system
  count          = local.create_efs ? 1 : 0
  creation_token = "${var.aws_resource_identifier}-token-modular"
  encrypted      = true

  lifecycle_policy {
    transition_to_ia = var.aws_efs_transition_to_inactive
  }

#  lifecycle {
#    prevent_destroy  = true
#  }

  tags = {
    Name = "${var.aws_resource_identifier}-efs-modular"
  }
}

resource "aws_security_group" "efs_security_group" {
  count  = local.create_efs ? 1 : 0
  name   = "SG for ${var.aws_resource_identifier} - EFS"
  description = "SG for ${var.aws_resource_identifier} - EFS"
  vpc_id      = data.aws_vpc.default.id
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
  tags = {
    Name = "${var.aws_resource_identifier}-efs-sg"
  }
}

resource "aws_security_group_rule" "efs_ingress_ports" {
  count  = local.create_efs ? 1 : 0
  type        = "ingress"
  description = "HTTP from VPC"
  from_port   = 80
  to_port     = 80
  protocol    = "tcp"
  cidr_blocks = ["0.0.0.0/0"]
  security_group_id = aws_security_group.efs_security_group[0].id
}

resource "aws_security_group_rule" "efs_tls_incoming_ports" {
  count  = local.create_efs ? 1 : 0
  type        = "ingress"
  description = "TLS from VPC"
  from_port   = 443
  to_port     = 443
  protocol    = "tcp"
  cidr_blocks = ["0.0.0.0/0"]
  security_group_id = aws_security_group.efs_security_group[0].id
}

resource "aws_efs_backup_policy" "efs_policy" {
  count          = var.aws_efs_enable_backup_policy && local.create_efs ? 1 : 0
  file_system_id = aws_efs_file_system.efs[0].id

  backup_policy {
    status = "ENABLED"
  }
}

resource "aws_efs_replication_configuration" "efs_rep_config" {
  count                 = var.aws_efs_create_replica && local.create_efs ? 1 : 0
  source_file_system_id = aws_efs_file_system.efs[0].id

  destination {
    region = local.replica_destination
  }
}

locals {
  create_efs_url = local.create_efs ? aws_efs_file_system.efs[0].dns_name : ""
  mount_efs_url  = var.aws_efs_mount_id != null ? data.aws_efs_file_system.mount_efs[0].dns_name : ""
  efs_url        = local.create_efs_url != "" ? local.create_efs_url : local.mount_efs_url
}
